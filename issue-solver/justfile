#!/usr/bin/env just --justfile

alias t := test
alias c := check-typing
alias l := lint
alias f := format

# ✅ Run all tests
test:
    uv run pytest

# 🧹 Lint the code
lint:
    uv run ruff check

# 🎨 Format the code
format:
    uv run ruff format

# ❇️ Check typing with mypy
check-typing:
    uv run mypy src tests --disable-error-code=import-untyped

set dotenv-load := true

# 🚀 Start the API
api-start:
    uv run fastapi dev src/issue_solver/webapi/main.py

# ▶️ Run standalone issue solver
run:
    uv run cudu help

# 🧩 Run cudu cli to solve issue
solve:
    sudo uv run cudu solve

# 📦 Package the webapi for AWS Lambda
build-lambda-package: clean
    uv export --frozen --no-dev --no-editable -o requirements.txt
    uv pip install --no-installer-metadata --no-compile-bytecode --python-platform x86_64-manylinux2014 --python 3.12 --target packages -r requirements.txt
    (cd packages && zip -r ../package.zip .)
    (cd src && zip -r ../package.zip issue_solver)

# 🧹 Clean up generated files
clean:
    rm -f package.zip
    rm -rf packages
    rm -f requirements.txt
